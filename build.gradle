plugins {
    id 'java'
    id "maven-publish"
    id "org.embulk.embulk-plugins" version "0.7.0"
}

repositories {
    mavenCentral()
}

// Set your own group ID
group = "org.embulk"

// Set the description of your plugin.
description = "Parses Excel files(xls, xlsx) read by other file input plugins."

version = "0.11.1-SNAPSHOT"

configurations {
    // We'd recommend to enable dependency locking so that you are aware of transitive dependencies.
    // See: https://docs.gradle.org/current/userguide/dependency_locking.html
    compileClasspath.resolutionStrategy.activateDependencyLocking()
    runtimeClasspath.resolutionStrategy.activateDependencyLocking()
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
    options.encoding = "UTF-8"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
    // "javadoc" JAR and "sources" JAR are required to publish the plugin to Maven Central.
    withJavadocJar()
    withSourcesJar()
}

dependencies {
    compileOnly "org.embulk:embulk-spi:0.11"

    implementation "org.embulk:embulk-util-config:0.5.0"
    implementation "org.embulk:embulk-util-file:0.2.0"
    implementation "org.embulk:embulk-util-timestamp:0.3.0"

    implementation group: 'org.apache.poi', name : 'poi', version: '5.2.3'
    implementation(group: 'org.apache.poi', name : 'poi-ooxml', version: '5.2.3') {
        exclude group: 'stax', module: 'stax-api'
    }    

    // The Embulk main packages are often required for testing.
    testImplementation "junit:junit:4.13.2"
    testImplementation "org.embulk:embulk-spi:0.11"
    testImplementation "org.embulk:embulk-core:0.11.0"
    testImplementation "org.embulk:embulk-deps:0.11.0"
    testImplementation "org.embulk:embulk-junit4:0.11.0"
    testImplementation "io.github.hishidama.embulk:hm-embulk-tester:0.1.+" // â˜…
}

embulkPlugin {
    // Set the plugin's main class.
    mainClass = "org.embulk.input.soap.SoapInputPlugin"

    // Set: "decoder", "encoder", "filter", "formatter", "guess", "input", "output", or "parser"
    category = "parser"

    // Set the "type" of the Embulk plugin used in Embulk's configuration YAML.
    // For instance, it would be "example" for "embulk-input-example".
    type = "poi_excel"
}

// It would be a good habit to contain the LICENSE file(s) at "META-INF/" in your plugin packages.
jar {
    metaInf {
        from rootProject.file("LICENSE")
    }
}
sourcesJar {
    metaInf {
        from rootProject.file("LICENSE")
    }
}
javadocJar {
    metaInf {
        from rootProject.file("LICENSE")
    }
}

gem {
  authors       = ["hishidama"]
  summary       = "[Apache POI Excel parser plugin for Embulk]"
  description   = "[Parses Excel files(xls, xlsx) read by other file input plugins.]"
  email         = ["hishi.dama@asahi.email.ne.jp"]
  licenses      = ["MIT"]
  homepage      = "https://github.com/hishidama/embulk-parser-poi_excel"
}
